plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'com.bmuschko.docker-spring-boot-application' version '6.1.3'
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

group = "com.sbrati"
version = "2.0.1"

repositories {
    jcenter()
    maven {
        url = project.properties["sbraticomUrl"]
        credentials {
            username = project.properties["sbraticomUsername"]
            password = project.properties["sbraticomPassword"]
        }
    }
}

dependencies {
    implementation platform('org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE')

    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.sbrati:spring-boot-starter-gcp-pubsub:1.0.6'

    implementation 'org.postgresql:postgresql:42.2.6'
    implementation 'org.flywaydb:flyway-core:5.2.4'

    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'commons-io:commons-io:2.6'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.testcontainers:postgresql:1.12.5"
}

sourceCompatibility = 11
targetCompatibility = 11

def gcpProjectId = project.properties["gcpProjectId"].toString()
def gcpImageName = "eu.gcr.io/${gcpProjectId}/${project.name}"
def sbratiImageName = "${project.properties["sbraticomDockerBaseImageName"].toString()}/${project.name}"
docker {
    springBootApplication {
        baseImage = 'openjdk:11-jdk-slim'
        images = [
                "${gcpImageName}:${project.version}",
                "${gcpImageName}:latest",
                "${sbratiImageName}:${project.version}",
                "${sbratiImageName}:latest"
        ]
        jvmArgs = ["-Xmx2048m"]
    }
}
